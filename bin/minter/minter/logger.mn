/**
 * @license
 * Copyright (c) 2024 Gauvain CHERY.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to
 * deal in the Software without restriction, including without limitation the
 * rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
 * sell copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice (including the next
 * paragraph) shall be included in all copies or substantial portions of the
 * Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 * FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
 * IN THE SOFTWARE.
 *
 * @module
 */

// TODO: make a complet generic logging library and put it in mint.logger

load mint.type

class Logger {
    enum Level {
        Off = 0
        Fatal = 100
        Error = 200
        Warning = 300
        Info = 400
        Debug = 500
        Trace = 600
        All = 0xFFFFFFFF
    }

    class Formatter {
        const def formatMessage(const self, const level, const message) {
            return "%s %s - %s - %s - %s\n" % (self.formatDate(System.Date.current()),
                System.TimeZone.current().getName(),
                self.formatLevel(level),
                mainModulePath(),
                message)
        }

        const def formatDate(const self, const date) {
            return date.toString('%d/%m/%Y %H:%M:%S.') + date.getMilliseconds()
        }

        const def formatLevel(const self, const level) {
            switch level {
            case is Logger.Level.Fatal:
                return 'FATAL'
            case is Logger.Level.Error:
                return 'ERROR'
            case is Logger.Level.Warning:
                return 'WARNING'
            case is Logger.Level.Info:
                return 'INFO'
            case is Logger.Level.Debug:
                return 'DEBUG'
            case is Logger.Level.Trace:
                return 'TRACE'
            }
            return 'UNKNOWN'
        }
    }

    class Handler {
        const def new(self, stream) {
            if self.stream = stream {
                return self
            }
        }

        const def write(const self, const level, const message) {
            self.stream.write(self.formatter.formatMessage(level, message))
            if defined self.stream.flush {
                self.stream.flush()
            }
        }

        const def setFormatter(self, formatter) {
            self.formatter = formatter
        }

        - final stream = null
        - final formatter = null
    }

	/**
	 * Disable object copy.
	 */
	- const clone

    const def setLevel(self, level) {
        self.level = level
    }

    const def addHandler(self, handler) {
        if Logger.Handler.isBaseOrSame(handler) {
            self.handlers << handler
        } else {
            self.handlers << Logger.Handler(handler)
        }
    }

    /**
     * Logs a `message` at the given `level`. Extra arguments are used to format
     * the message string.
     */
    const def log(const self, const level, const message, ...) {
        if self.level >= level {
            const logMessage = message % va_args
            for var handler in self.handlers {
                handler.write(level, logMessage)
            }
        }
    }

    /**
     * Logs a `message` with the {@see Level.Fatal} level. Extra arguments are
     * used to format the message string.
     */
    const def fatal(const self, const message, ...) {
        self.log(Logger.Level.Fatal, message, *va_args)
    }

    /**
     * Logs a `message` with the {@see Level.Error} level. Extra arguments are
     * used to format the message string.
     */
    const def error(const self, const message, ...) {
        self.log(Logger.Level.Error, message, *va_args)
    }

    /**
     * Logs a `message` with the {@see Level.Warning} level. Extra arguments are
     * used to format the message string.
     */
    const def warning(const self, const message, ...) {
        self.log(Logger.Level.Warning, message, *va_args)
    }

    /**
     * Logs a `message` with the {@see Level.Info} level. Extra arguments are
     * used to format the message string.
     */
    const def info(const self, const message, ...) {
        self.log(Logger.Level.Info, message, *va_args)
    }

    /**
     * Logs a `message` with the {@see Level.Debug} level. Extra arguments are
     * used to format the message string.
     */
    const def debug(const self, const message, ...) {
        self.log(Logger.Level.Debug, message, *va_args)
    }

    /**
     * Logs a `message` with the {@see Level.Trace} level. Extra arguments are
     * used to format the message string.
     */
    const def trace(const self, const message, ...) {
        self.log(Logger.Level.Trace, message, *va_args)
    }

    - final level = 0
    - final handlers = []
}

$@ log = Logger()
