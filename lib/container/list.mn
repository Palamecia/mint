/**
 * This module provides the {List} class which store objects sequentially in
 * memory.
 */

load mint.type
load exception.container

/**
 * This class creates a list which stores a liste of values and provides an
 * indexes based access and always stores its items sequentially in memory.
 */
class List {
	/**
	 * Creates a new instance of {List}. If `values` is given, the list will be
	 * initialized with the given values.
	 */
	def new(self, values = []) {
		for item in array(values) {
			self.values << item
		}
		return self
	}

	/**
	 * Returns `true` if `other` is equal to this list; otherwise returns `false`.
	 * 
	 * Two lists are considered equal if they contain the same values in the same
	 * order.
	 * 
	 * This function requires the values to have an implementation of the `!=`
	 * operator.
	 */
	def ==(self, other) {
		if self.size() == other.size() {
			for i in 0...self.size() {
				if self[i] != other[i] {
					return false
				}
			}
			return true
		}
		return false
	}

	/**
	 * Returns `true` if `other` is not equal to this list; otherwise returns `false`.
	 * 
	 * Two lists are considered equal if they contain the same values in the same
	 * order.
	 * 
	 * This function requires the values to have an implementation of the `!=`
	 * operator.
	 */
	def !=(self, other) {
		if self.size() == other.size() {
			for i in 0...self.size() {
				if self[i] != other[i] {
					return true
				}
			}
			return false
		}
		return true
	}

	/**
	 * Returns a list which is the result of concatenating `self` and `value`.
	 */
	def +(self, value) {
		result = List(self.values)
		for item in array(value) {
			result << item
		}
		return result
	}

	/**
	 * Returns a list with each occurence of `value` removed.
	 */
	def -(self, value) {
		result = List(self.values)
		for item in array(value) {
			result.removeAll(item)
		}
		return result
	}

	/**
	 * Returns a list with each elements of this list repeated `value` times.
	 */
	def *(self, value) {
		return List(self.values * number(value))
	}

	/**
	 * Appends `value` to the list and returns `self`.
	 */
	def <<(self, value) {
		self.values << value
		return self
	}

	/**
	 * Returns a list corresponding to the intersection `self` and `value`.
	 */
	def &(self, value) {
		return List(self.values & array(value))
	}

	/**
	 * Returns the elements at the specified `index` in the list. If `index` is
	 * negative, the position is relative to the end of the list.
	 * 
	 * The `index` parameter can either be a `number` or an `iterator`. This
	 * operator can then be used with a range to extract a sub-set of elements
	 * between two positions.
	 */
	def [](self, index) {
		if index >= self.size() or self.size() + index < 0 {
			raise OutOfRange(self, index)
		}
		return self.values[index]
	}

	/**
	 * Replace the elements at the specified `index` in the list with the
	 * elements of `value`. If `index` is negative, the position is relative
	 * to the end of the list.
	 * 
	 * The `index` parameter can either be a `number` or an `iterator`. This
	 * operator can then be used with a range to replace a sub-set of elements
	 * between two positions.
	 */
	def []=(self, index, value) {
		if index >= self.size() or self.size() + index < 0 {
			raise OutOfRange(self, index)
		}
		return self.values[index] = value
	}

	/**
	 * Returns the first element of the list. If the list is empty, an instance
	 * of {NoItem} is raised.
	 */
	def first(self) {
		if self.isEmpty() {
			raise NoItem(self, 'get')
		}
		return self.values[0]
	}

	/**
	 * Returns the last element of the list. If the list is empty, an instance
	 * of {NoItem} is raised.
	 */
	def last(self) {
		if self.isEmpty() {
			raise NoItem(self, 'get')
		}
		return self.values[-1]
	}
	
	/**
	 * Returns an `array` containing each elements of the list.
	 */
	def data(self) {
		return self.values
	}

	/**
	 * Returns an `iterator` on the elements of the list.
	 */
	def in(self) {
		for item in self.values {
			yield item
		}
	}

	/**
	 * Returns `true` if the list contains `value`; otherwise returns `false`.
	 */
	def in(self, value) {
		for item in self.values {
			if item == value {
				return true
			}
		}
		return false
	}

	/**
	 * Apply the `func` function to each elements of the list.
	 */
	def each(self, func) {
		for item in self.values {
			func(item)
		}
	}

	/**
	 * Converts the list to an `array`.
	 */
	def toArray(self) {
		return self.values
	}

	/**
	 * Converts the list to an `hash`.
	 */
	def toHash(self) {
		return hash(self.values)
	}

	/**
	 * Returns `true` if the list contains `value`; otherwise returns `false`.
	 */
	def contains(self, value) {
		return self.values.contains(value)
	}

	/**
	 * Returns the number of occurrences of `value` in the list.
	 */
	def count(self, value) {
		found = 0
		for item in self.values {
			if item == value {
				found++
			}
		}
		return found
	}

	/**
	 * Returns the number of elements in the list.
	 */
	def size(self) {
		return self.values.size()
	}
	
	/**
	 * Returns `true` if the list is empty; otherwise returns `false`.
	 */
	def isEmpty(self) {
		return self.values.isEmpty()
	}

	/**
	 * Returns the index position of the first occurrence of `value` in the list,
	 * searching forward from index position `from`. Returns `none` if no element
	 * matched.
	 */
	def indexOf(self, value, from = 0) {
		return self.values.indexOf(value, from)
	}

	/**
	 * Returns the index position of the last occurrence of `value` in the list,
	 * searching backward from index position `from`. Returns `none` if no 
	 * element matched.
	 */
	def lastIndexOf(self, value, from = none) {
		return self.values.lastIndexOf(value, from)
	}

	/**
	 * Replace the elements at the specified `index` in the list with the
	 * elements of `value`. If `index` is negative, the position is relative
	 * to the end of the list. If `index` is not a valid index of the list (ie.
	 * `index` is not contained in the range 0...{@see size}), an instance of
	 * {OutOfRange} is raised.
	 */
	def replace(self, index, value) {
		if index >= self.size() or self.size() + index < 0 {
			raise OutOfRange(self, index)
		}
		self.values[index] = value
	}

	/**
	 * Inserts `value` in the list at the specified `index`.  If `index` is
	 * negative, the position is relative to the end of the list. If `index` is
	 * not a valid index of the list (ie. `index` is not contained in the range
	 * 0...{@see size}), an instance of {OutOfRange} is raised.
	 */
	def insert(self, index, value) {
		if index < 0 {
			index = self.values.size() + index
		}
		if 0 <= index and index <= self.values.size() {
			if index == 0 {
				self.values := [value] + self.values
			} elif index < self.values.size() {
				self.values := self.values[0...index] + value + self.values[index...self.values.size()]
			} else {
				self.values << value
			}
		} else {
			raise OutOfRange(self, index)
		}
	}

	/**
	 * Inserts `value` at the end of the list.
	 */
	def append(self, value) {
		self.values << value
	}

	/**
	 * Inserts `value` at the beginning of the list.
	 */
	def prepend(self, value) {
		self.values := [value] + self.values
	}

	/**
	 * Moves the element at index position `from` to index position `to`.
	 */
	def move(self, from, to) {
		if from >= self.size() or self.size() + from < 0 {
			raise OutOfRange(self, from)
		}
		if to >= self.size() or self.size() + to < 0 {
			raise OutOfRange(self, to)
		}
		memory = self.values[from]
		self.values[from] = self.values[to]
		self.values[to] = memory
	}

	/**
	 * Removes the first occurence of `value` in the list.
	 */
	def removeOne(self, value) {
		for i in 0...self.values.size() {
			if self.values[i] == value {
				self.values.remove(i)
				return true
			}
		}
		return false
	}

	/**
	 * Removes all the occurences of `value` in the list.
	 */
	def removeAll(self, value) {
		if self.removeOne(value) {
			while self.removeOne(value) {}
			return true
		}
		return false
	}

	/**
	 * Removes the element at the given `index` in the list.
	 */
	def remove(self, index) {
		if index >= self.size() or self.size() + index < 0 {
			raise OutOfRange(self, index)
		}
		self.values.remove(index)
	}

	/**
	 * Removes all the elements of the list.
	 */
	def clear(self) {
		self.clear()
	}

	- values = []
}
