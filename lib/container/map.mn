load mint.type
load exception.container
load container.redblacktree

class Map {
	def new(self) {
		self.root = RedBlackTree()
		return self
	}

	def new(self, values) {
		self.root = RedBlackTree()
		for key, value in hash(values) {
			self.insert(key, value)
		}
		return self
	}

	def [](self, key) {
		node = self.root.find(Map.Item(key))
		if defined node {
			return node.value
		}
		raise InvalidKey(key)
	}

	def []=(self, key, value) {
		self.set(key, value)
		return value
	}

	def in(self) {
		for item in self.data() {
			yield item
		}
	}

	def in(self, key) {
		node = self.root.find(Map.Item(key))
		return defined node
	}

	def each(self, func) {

		unpack_func = func[2]

		if defined unpack_func {
			self.root.each(def [unpack_func] (item) {
				unpack_func(item.key, item.value)
			})
		} else {
			self.root.each(def [func] (item) {
				func((item.key, item.value))
			})
		}
	}

	def data(self) {
		data = {}
		self.root.each(def [data] (item) {
			data[item.key] = item.value
		})
		return data
	}

	def items(self) {
		items = []
		self.root.each(def [items] (item) {
			items << (item.key, item.value)
		})
		return items
	}

	def keys(self) {
		keys = []
		self.root.each(def [keys] (item) {
			keys << item.key
		})
		return keys
	}

	def values(self) {
		values = []
		self.root.each(def [values] (item) {
			values << item.value
		})
		return values
	}

	def toArray(self) {
		return self.keys()
	}

	def toHash(self) {
		return self.data()
	}

	def insert(self, key, value) {
		self.root.insert(Map.Item(key, value))
	}

	def get(self, key, default_value = none) {
		node = self.root.find(Map.Item(key))
		if defined node {
			return node.value
		}
		return default_value
	}

	def set(self, key, value) {
		node = self.root.find(Map.Item(key))
		if defined node {
			node.value = value
		} else {
			self.root.insert(Map.Item(key, value))
		}
	}

	def remove(self, key) {
		return self.root.remove(Map.Item(key))
	}

	def clear(self) {
		self.root = RedBlackTree()
	}

	def size(self) {
		count = 0
		self.root.each(def [count] (item) {
			count := count + 1
		})
		return count
	}

	def isEmpty(self) {
		return self.root.isEmpty()
	}

	- class Item {
		def new(self, key, value = none) {
			self.key = key
			self.value = value
			return self
		}

		def <(self, other) {
			return self.key < other.key
		}

		+ key = none
		+ value = none
	}

	- root = null
}
