/**
 * @license
 * Copyright (c) 2025 Gauvain CHERY.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to
 * deal in the Software without restriction, including without limitation the
 * rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
 * sell copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice (including the next
 * paragraph) shall be included in all copies or substantial portions of the
 * Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 * FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
 * IN THE SOFTWARE.
 *
 * @module
 * This module provides the {Event.File} class which provides a waitable file
 * implementation.
 */

load serializer.datastream

package Event {
	/**
	 * This class provides a waitable file implementation that trigger an event
	 * when the file is modified.
	 * 
	 * This class only provides basic file mecanisme as write and read raw data.
	 * To perform extanded operations on files, use {System.File}.
	 * 
	 * > This type is not copyable
	 */
	class File {
		/**
		 * This enum describes the differents modifications that can be watched
		 * on a file.
		 */
		enum Change {
			/// The file name has changed
			Name = 0x01
			/// The content of the file has changed
			Data = 0x02
			/// The attributes of the file has changed
			Attributes = 0x04
		}

		/**
		 * Creates a new file. The file is automaticaly opened.
		 * 
		 * The `path` parameter must be a string containing the path to the file
		 * to open.
		 * 
		 * The `mode` parameter must be a string containing one or more flag
		 * character describing how to open the file. The following flag
		 * characters are supported:
		 * | Flag | Description |
		 * |------|-------------|
		 * | `r` | The openned file is readable. |
		 * | `w` | The openned file is writeable. If the file exists, it will be truncated. |
		 * | `a` | The openned file is writeable. If the file exists, data will be appended at the end of the file. |
		 * | `+` | If the file is readable, write operations are also available. If the file is writable, read operations are also available. |
		 * 
		 * The `flags` parameter must be a combination of values from the
		 * {Event.File.Change} enum and describes the watched events types.
		 * 
		 * If no file can be openned at the given `path` with the given `mode`,
		 * `none` is returned.
		 */
		const def new(self, path, mode, flags = Event.File.Change.Data) {
			(self.fileHandle, self.eventHandle) := Event.File.g_lib.call('mint_file_create', path, mode, flags)
			if defined self.fileHandle and defined self.eventHandle {
				return self
			}
		}

		/**
		 * Cleans up the file instance.
		 */
		const def delete(self) {
			if defined self.fileHandle {
				Event.File.g_lib.call('mint_file_close_file', self.fileHandle)
			}
			if defined self.eventHandle {
				Event.File.g_lib.call('mint_file_close_event', self.eventHandle)
			}
		}

		/**
		 * Disable object copy.
		 */
		- const clone

		/**
		 * Returns the data contained in the file from the current position to
		 * the end of the file. This method moves the current position to the
		 * end of the file.
		 * 
		 * The file's data is contained in an instance of {Serializer.DataStream}.
		 */
		const def read(self) {
			var stream = Serializer.DataStream()
			Event.File.g_lib.call('mint_file_read', self.fileHandle, self.eventHandle, stream.to_std_vector_uint8_t())
			return stream
		}
		
		/**
		 * Writes the `data` in the file at the current position. This method
		 * moves the current position to the end of the `data`.
		 */
		const def write(self, data) {
			var stream = Serializer.DataStream(data)
			Event.File.g_lib.call('mint_file_write', self.fileHandle, stream.to_std_vector_uint8_t())
		}

		/**
		 * Waits until the file is modified. If `timeout` is given, the wait
		 * stop after `timeout` milliseconds if the file is still not modified.
		 * 
		 * Returns `true` if the file is modified; otherwise returns `false` if
		 * the wait timed out.
		 * 
		 * After a call to this method, the event is reseted.
		 */
		const def wait(self, timeout = none) {
			return Event.File.g_lib.call('mint_file_wait', self.eventHandle, timeout)
		}

		/**
		 * Returns the handle used by {Event.Watcher}.
		 */
		const def getHandle(const self) {
			return self.eventHandle
		}

		/**
		 * Reset the internal event state
		 */
		const def reset(self) {
			self.read()
		}

		/// Global library handle.
		- @g_lib = lib('libmint-event')

		/// Internal file access handle.
		- final fileHandle = none

		/// Internal handle.
		- final eventHandle = none
	}
}
