/**
 * This module provides a set of functions to perform extra operations on hashes.
 */

/**
 * Returns the value associated to `key` if contained in `self`; otherwise
 * returns `default_value`. The content of `self` is not modified.
 */
$ def get(const self, key, default_value = none) {
    if key in self {
        return self[key]
    }
    return default_value
}

/**
 * Returns `true` if `self` contains the key `key`; otherwise returns `false`.
 */
$ def contains(const self, key) {
    return self.in(key)
}

/**
 * Returns an iterator on each item contained in `self`.
 */
$ def items(const self) {
    return self.in()
}

/**
 * Returns an iterator on each key contained in `self`.
 */
$ def keys(const self) {
    for key, value in self {
        yield key
    }
}

/**
 * Returns an iterator on each value contained in `self`.
 */
$ def values(const self) {
    for key, value in self {
        yield value
    }
}
