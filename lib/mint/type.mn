/**
 * This module provides functions to manage types.
 */

/**
 * Converts `value` to `number`. If `value` is already a `number`, it is 
 * returned; otherwise, if it provides a `toNumber` method, the result of the
 * method call is returned; otherwise, a built-in cast is performed.
 */
$ def [g_lib = lib('libmint-mint')] number(value = 0) {
	if typeof value == 'number' {
		return value
	}
	if defined value.toNumber {
		return value.toNumber()
	}
	return g_lib.call('mint_type_to_number', value)
}

/**
 * Fallback to {number} function when calling `toNumber` on an object without
 * the method.
 */
@const toNumber = number

/**
 * Converts `value` to `boolean`. If `value` is already a `boolean`, it is
 * returned; otherwise, if it provides a `toBoolean` method, the result of the
 * method call is returned; otherwise, a built-in cast is performed.
 */
$ def [g_lib = lib('libmint-mint')] boolean(value = false) {
	if typeof value == 'boolean' {
		return value
	}
	if defined value.toBoolean {
		return value.toBoolean()
	}
	return g_lib.call('mint_type_to_boolean', value)
}

/**
 * Fallback to {boolean} function when calling `toBoolean` on an object without
 * the method.
 */
@const toBoolean = boolean

/**
 * Converts `value` to `string`. If `value` is already a `string`, it is
 * returned; otherwise, if it provides a `toString` method, the result of the
 * method call is returned; otherwise, a built-in cast is performed.
 */
$ def [g_lib = lib('libmint-mint')] string(value = '') {
	if typeof value == 'string' {
		return value
	}
	if defined value.toString {
		return value.toString()
	}
	return g_lib.call('mint_type_to_string', value)
}

/**
 * Fallback to {string} function when calling `toString` on an object without
 * the method.
 */
@const toString = string

/**
 * Converts `value` to `regex`. If `value` is already a `regex`, it is
 * returned; otherwise, if it provides a `toRegex` method, the result of the
 * method call is returned; otherwise, a built-in cast is performed.
 */
$ def [g_lib = lib('libmint-mint')] regex(value = /.*/) {
	if typeof value == 'regex' {
		return value
	}
	if defined value.toRegex {
		return value.toRegex()
	}
	return g_lib.call('mint_type_to_regex', value)
}

/**
 * Fallback to {regex} function when calling `toRegex` on an object without
 * the method.
 */
@const toRegex = regex

/**
 * Converts `value` to `array`. If `value` is already a `array`, it is
 * returned; otherwise, if it provides a `toArray` method, the result of the
 * method call is returned; otherwise, a built-in cast is performed.
 */
$ def [g_lib = lib('libmint-mint')] array(value = []) {
	if typeof value == 'array' {
		return value
	}
	if defined value.toArray {
		return value.toArray()
	}
	return g_lib.call('mint_type_to_array', value)
}

/**
 * Fallback to {array} function when calling `toArray` on an object without
 * the method.
 */
@const toArray = array

/**
 * Converts `value` to `hash`. If `value` is already a `hash`, it is
 * returned; otherwise, if it provides a `toHash` method, the result of the
 * method call is returned; otherwise, a built-in cast is performed.
 */
$ def [g_lib = lib('libmint-mint')] hash(value = {}) {
	if typeof value == 'hash' {
		return value
	}
	if defined value.toHash {
		return value.toHash()
	}
	return g_lib.call('mint_type_to_hash', value)
}

/**
 * Fallback to {hash} function when calling `toHash` on an object without
 * the method.
 */
@const toHash = hash

/**
 * Creates a new instance of `type`. The `type` parameter can either be a cast
 * function of this module, a class or a type name. Extra arguments are passed
 * to the cast function or constructor called.
 * 
 * Example :
 * ```mint
 * other = new(typeof value)
 * ```
 * 
 * > Note : if `type` is an object, the type of `type` is used instead.
 */
$ def new(type, ...) {

	switch type {
	case is number:
		return type(*va_args)
	case is boolean:
		return type(*va_args)
	case is string:
		return type(*va_args)
	case is regex:
		return type(*va_args)
	case is array:
		return type(*va_args)
	case is hash:
		return type(*va_args)
	}

	if typeof type != 'string' {
		type = typeof type
	}

	for symbol in type.split('.') {
		if defined prototype {
			prototype = prototype.$(symbol)
		} else {
			prototype = $(symbol)
		}
	}

	return prototype(*va_args)
}

/**
 * Returns the owner class of the member named `member_name` in the class `type`.
 * If no owner class can be found, `none` is returned.
 */
$ def [g_lib = lib('libmint-mint')] getMemberOwner(type, member_name) {
	return g_lib.call('mint_type_get_member_owner', type, member_name)
}

/**
 * Set the owner class of the member named `member_name` in the class `type` to
 * `owner`. Returns `true` if the owner was successfuly changed; otherwise
 * returns `false`.
 */
$ def [g_lib = lib('libmint-mint')] setMemberOwner(type, member_name, owner) {
	return g_lib.call('mint_type_set_member_owner', type, member_name, owner)
}

/**
 * Returns `true` if the built-in copy operation can be applied to `type`; otherwise
 * returns `false`. The `type` parameter can either be a class or an object.
 */
$ def [g_lib = lib('libmint-mint')] isCopyable(type) {
	return g_lib.call('mint_type_is_copyable', type)
}

/**
 * Disable the built-in copy operation for `type`. Returns `true` if the built-in
 * copy operation can be disabled; otherwise returns `false`. The `type`
 * parameter can either be a class or an object.
 */
$ def [g_lib = lib('libmint-mint')] disableCopy(type) {
	return g_lib.call('mint_type_disable_copy', type)
}

/**
 * Copy the value of `value`. If `value` provides a `clone` method, the result
 * of the method call is returned; otherwise a built-in copy operation is
 * performed.
 */
$ def [g_lib = lib('libmint-mint')] copy(value) {

	if defined value.clone {
		return value.clone()
	}

	return g_lib.call('mint_type_deep_copy', value)
}

/**
 * Returns `true` if `object` is a non instanciated class; otherwise returns `false`.
 */
$ def [g_lib = lib('libmint-mint')] isClass(object) {
	return g_lib.call('mint_type_is_class', object)
}

/**
 * Returns `true` if `object` is an instanciated object; otherwise returns `false`.
 */
$ def [g_lib = lib('libmint-mint')] isObject(object) {
	if defined object {
		return g_lib.call('mint_type_is_object', object)
	}
	return false
}

/**
 * Returns an array containing the base classes of `type`.
 */
$ def [g_lib = lib('libmint-mint')] super(type) {
	return g_lib.call('mint_type_super', type)
}

/**
 * Returns `true` if `base` is a base class of `type`; otherwise returns `false`.
 */
$ def [g_lib = lib('libmint-mint')] isBaseOf(base, type) {
	return g_lib.call('mint_type_is_base_of', base, type)
}
