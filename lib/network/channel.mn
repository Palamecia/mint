load network.serializers.text
load network.scheduler
load mint.lockguard
load system.mutex

package Network {

	/*!
	* @class Network.Channel
	*/
	class Channel {
		def new(self, endpoint, _Serializer = TextSerializer) {
			self.endpoint = endpoint
			self.request = Channel.DataQueue(_Serializer)
			self.reply = Channel.DataQueue(_Serializer)
			return self
		}

		def connect(self) {
			return self.endpoint.connect()
		}

		def listen(self) {
			return self.endpoint.listen()
		}

		def watch(self) {
			return Network.Scheduler.instance().watch(self)
		}

		def unwatch(self) {
			return Network.Scheduler.instance().remove(self)
		}

		def write(self, data) {
			return self.request.push(data)
		}

		def read(self) {
			return self.reply.pop()
		}

		def getDescriptor(self) {
			return self.endpoint.getDescriptor()
		}

		def getHandle(self) {
			return self.endpoint.getHandle()
		}

		def getEndpoint(self) {
			return self.endpoint
		}

		def close(self) {
			return self.endpoint.close()
		}

		def getStatus(self) {
			return self.endpoint.getStatus()
		}

		def getEvents(self) {
			switch self.endpoint.getStatus() {
			case EndPoint.Status.unknown:
			case EndPoint.Status.disconnected:
			case EndPoint.Status.closed:
			case EndPoint.Status.error:
				return 0

			case EndPoint.Status.connecting:
				return Scheduler.PollEvent.write

			case EndPoint.Status.listening:
				return Scheduler.PollEvent.accept

			case EndPoint.Status.connected:
				return Scheduler.PollEvent.read | Scheduler.PollEvent.write | Scheduler.PollEvent.close | Scheduler.PollEvent.error
			}
		}

		def isNonBlocking(self) {
			return self.endpoint.isNonBlocking()
		}

		def setNonBlocking(self, enabled) {
			return self.endpoint.setNonBlocking(enabled)
		}

		def notifyReadable(self) {
			if buffer = self.endpoint.read() {
				self.reply.write(buffer)
				if defined self.onDataReceived and not self.reply.isEmpty() {
					self.onDataReceived(self)
				}
			}
		}

		def notifyWritable(self) {
			if self.endpoint.getStatus() is EndPoint.Status.connecting {
				self.endpoint.finalizeConnexion()
			} elif buffer = self.request.read() {
				success, count = self.endpoint.write(buffer)
				if success {
					self.request.remove(count)
				}
			}
		}

		def notifyConnexion(self) {
			self.reply.push(self.endpoint.accept())
			if defined self.onDataReceived {
				self.onDataReceived(self)
			}
		}

		def notifyClosed(self) {
			self.endpoint.close()
			if defined self.onClose {
				self.onClose(self)
			}
		}

		def notifyError(self) {
			self.endpoint.setStatus(EndPoint.Status.error)
			if defined self.onError {
				self.onError(self)
			}
		}

		+ onDataReceived = none
		+ onError = none
		+ onClose = none

		- class DataQueue {
			def new(self, _Serializer) {
				self.serializer = _Serializer()
				self.mutex = System.Mutex()
				return self
			}

			def write(self, data) {
				lock = LockGuard(self.mutex)
				self.serializer.write(data)
			}

			def read(self) {
				lock = LockGuard(self.mutex)
				return self.serializer.read()
			}

			def remove(self, count) {
				lock = LockGuard(self.mutex)
				self.serializer.remove(count)
			}

			def isEmpty(self) {
				lock = LockGuard(self.mutex)
				return not self.serializer.containsMessage()
			}

			def push(self, data) {
				lock = LockGuard(self.mutex)
				self.serializer.appendMessage(data)
			}

			def pop(self) {
				lock = LockGuard(self.mutex)
				return self.serializer.nextMessage()
			}

			- serializer = null
			- mutex = null
		}

		- endpoint = null
		- request = null
		- reply = null
	}

}
