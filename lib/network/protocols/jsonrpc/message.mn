/**
 * @license
 * Copyright (c) 2024 Gauvain CHERY.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to
 * deal in the Software without restriction, including without limitation the
 * rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
 * sell copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice (including the next
 * paragraph) shall be included in all copies or substantial portions of the
 * Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 * FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
 * IN THE SOFTWARE.
 *
 * @module
 */

load serializer.stringstream
load serializer.datastream
load serializer.jsonstream
load mint.string

package Network {
	package JsonRpc {

		class Message {
			const def new(self) {
				return self
			}

			const def new(self, body, header = true) {
				switch typeof body{
				case 'string':
					self.header = header
					self.body = body
					return self
				case 'JsonStream':
					self.header = header
					self.body = body.read()
					return self
				case 'Network.JsonRpc.Message':
					return body
				}
			}

			@const def fromHash(hash, header = true) {
				if defined hash {
					var body = ''
					JsonStream(StringStream(body)).write(body)
					return Network.JsonRpc.Message(stream.read(), header)
				}
			}

			const def in(const self, const key) {
				return self.body.in(key)
			}

			const def in(const self) {
				for item in self.body {
					yield item
				}
			}

			const def [](const self, const key) {
				return self.body[key]
			}

			const def []=(self, const key, value) {
				return self.body[key] = value
			}

			const def getBody(const self) {
				return self.body
			}

			const def setBody(self, body) {
				self.body = body
			}

			const def isHeaderEnabled(const self) {
				return self.header
			}

			const def setHeaderEnabled(const self, enabled) {
				self.header = enabled
			}

			/**
			 * Returns the content of the message as a string.
			 */
			const def toString(const self) {
				var str = ''
				var stream = JsonStream(StringStream(str))
				stream.setIndented(false)
				stream.write(self.body)
				return str
			}

			const def encode(self) {

				var buffer = ''

				if self.header {
					buffer << 'Content-Length: %d\r\n\r\n' % self.body.utf8ByteCount()
				}

				buffer << self.body
				return DataStream(buffer)
			}

			@const def decode(stream, length) {
				if length {

					var self = Network.JsonRpc.Message()
					var buffer = stream.getString(0, length)

					var start = buffer.indexOf('{')
					if defined start {
						self.header = start > 0
						self.body = JsonStream(buffer.substring(start)).read()
						return self
					}
				}
			}

			- header = false
			- body = none
		}
	}
}
