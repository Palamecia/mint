/**
 * @license
 * Copyright (c) 2024 Gauvain CHERY.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to
 * deal in the Software without restriction, including without limitation the
 * rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
 * sell copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice (including the next
 * paragraph) shall be included in all copies or substantial portions of the
 * Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 * FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
 * IN THE SOFTWARE.
 *
 * @module
 * This module provides the {Network.DataSerializer} class which implement the
 * serializer interface for fixed length messages on a stream based end point such
 * as {Network.TcpIp}.
 */

load serializer.datastream
load exception.network

package Network {
	/**
	 * This class provides a serializer for fixed length messages. The message
	 * length is passed to the constructor. This class can not be passed to an
	 * instance of {Network.AsynchronousChannel} as is but must be used with a
	 * decorator.
	 * 
	 * **Example**:
	 * ```mn
	 * channel = Network.AsynchronousChannel(endpoint, def { return Network.DataSerializer(1024) })
	 * ```
	 */
	class DataSerializer {
		/**
		 * Creates a new serializer for messages of fixed length `length`.
		 */
		const def new(self, length) {
			self.stream = Serializer.DataStream()
			self.length = length
			return self
		}

		/**
		 * Appends the message object provided by `message` to the serializer's
		 * buffer. The `message` parameter must convert to a byte sequence with
		 * a length equal to the fixed length configured for this serializer.
		 */
		const def appendMessage(self, message) {
			if defined message {
				self.stream.write(message)
			}
		}

		/**
		 * Returns the next message in the serializer's buffer as an array of
		 * {uint8} with a length equal to the fixed length configured for this
		 * serializer or `none` if the buffer contains less than this configured
		 * amount of bytes. The data used to create the message is then removed
		 * from the buffer.
		 */
		const def nextMessage(self) {
			return self.stream.readUInt8(self.length)
		}

		/**
		 * Returns `true` if the serializer's buffer contains at least the
		 * configured amount of bytes; otherwise returns `false`.
		 */
		const def containsMessage(const self) {
			return self.stream.containsUInt8(self.length)
		}

		/**
		 * Attempts to read data from the end point. Returns `true` if data was
		 * receive; otherwise returns `false`.
		 */
		const def read(self, endpoint) {
			if var buffer = endpoint.read() {
				self.stream.write(buffer)
				return true
			}
			return false
		}

		/**
		 * Attempts to write data to the end point. Returns `true` if the data
		 * was successfully sent; otherwise returns `false`. After this call,
		 * the sent data is removed from the buffer.
		 */
		const def write(self, endpoint) {
			if not self.stream.isEmpty() {
				var success, var count = endpoint.write(self.stream)
				if success {
					self.stream.remove(count)
					return true
				}
			}
			return false
		}

		/**
		 * Attempts to accept a connection on the end point. This method raises
		 * an instance of {Exception.InvalidNetworkOperation}.
		 */
		const def accept(self, endpoint) {
			raise Exception.InvalidNetworkOperation('the socket can not accept', self)
		}

		/**
		 * Returns `true` if the serializer's buffer is empty; otherwise
		 * returns `false`.
		 */
		const def isEmpty(const self) {
			return self.stream.isEmpty()
		}

		/// Internal buffer.
		- final stream = null

		/// Internal fixed length.
		- final length = 0
	}
}
