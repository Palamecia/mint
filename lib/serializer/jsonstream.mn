/**
 * @license
 * Copyright (c) 2025 Gauvain CHERY.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to
 * deal in the Software without restriction, including without limitation the
 * rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
 * sell copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice (including the next
 * paragraph) shall be included in all copies or substantial portions of the
 * Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 * FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
 * IN THE SOFTWARE.
 *
 * @module
 * This module provides the {Serializer.DataStream} class which allow to read and
 * write JSON documents.
 */

load mint.lang
load mint.type
load exception.exception
load serializer.documentstream

package Serializer {

/**
 * This class class provides a way to read and write JSON documents.
 */
class JsonStream : Serializer.DocumentStream {
	/**
	 * Returns the content of `str` with special characters escaped to be
	 * inserted in a JSON document.
	 */
	@const def escapeString(str) {
		var escaped_string = ''
		for c in str {
			switch c {
			case '"':
		        escaped_string << '\\"'
		        break
		    case '\\':
		        escaped_string << '\\\\'
		        break
		    case '\b':
		        escaped_string << '\\b'
		        break
		    case '\f':
		        escaped_string << '\\f'
		        break
		    case '\n':
		        escaped_string << '\\n'
		        break
		    case '\r':
		        escaped_string << '\\r'
		        break
		    case '\t':
		        escaped_string << '\\t'
		        break
			default:
				escaped_string << c
			}
		}
		return escaped_string
	}

	/**
	 * Returns `true` if the stream indent the JSON when writing; otherwise
	 * returns `false`.
	 */
	const def isIndented(const self) {
		return self.indented
	}

	/**
	 * Enables the indentation of the JSON writed to the stream. If `indented`
	 * is `true`, the JSON will be indented; otherwise the JSON will be
	 * compact.
	 * 
	 * Indented example:
	 * ```json
	 * {
	 *     "array": [
	 *         {
	 *             "object": {
	 *                 "str": "value",
	 *                 "int": 32,
	 *                 "bool": false,
	 *                 "null": null
	 *             }
	 *         }
	 *     ]
	 * }
	 * ```
	 * 
	 * Compact example:
	 * ```json
	 * {"array": [{"object": {"str": "value","int": 32,"bool": false,"null": null}}]}
	 * ```
	 * 
	 * > By default, the data is indented.
	 */
	const def setIndented(self, indented) {
		self.indented = indented
	}

	/**
	 * Reads the content of the stream and return it as an hash. If the content
	 * of the stream is not a valid JSON, an instance of
	 * {Serializer.DocumentStream.InvalidDocument} is raised.
	 */
	override const def read(self) {
		var parser = Serializer.JsonStream.Parser(self)
		return parser.parse()
	}

	/**
	 * Writes the content of `data` to the stream. The indentation mode set by
	 * {@see setIndented} is used to adapt the format. The content is converted
	 * to JSON using the {@see valueToJson} function.
	 */
	override const def write(self, data) {
		Serializer.DocumentStream.write(self, self.writeJson(data))
	}

	/**
	 * Reads an object from the stream and stores it in `data`, then returns a
	 * reference to the stream.
	 */
	override const def >>(self, data) {
		data := self.read()
		return self
	}

	/**
	 * Writes the content of `data` to the stream using the {@see write}
	 * function, then returns a reference to the stream.
	 */
	override const def <<(self, data) {
		self.write(data)
		return self
	}

	/**
	 * Writes the element `value` to the stream with the current indentation.
	 * The element is converted to JSON using {@see valueToJson}.
	 */
	- final const def writeJson(self, value) {
		return ('\t' * self.indentationLevel) + self.valueToJson(value)
	}

	/**
	 * Converts the `value` element to JSON as follow:
	 * * hashes are converted to JSON objects.
	 * * arrays are converted to JSON arrays.
	 * * numbers are converted to JSON numeric values.
	 * * strings are converted to JSON string values using {@see escapeString}.
	 * * booleans are converted to JSON boolean values.
	 * * `null` is converted to JSON null value.
	 * * for other types, the conversion methods are tested in the following
	 *   order: `toBoolean`, `toNumber`, `	toString`. If no conversion method
	 *   is found, the data is cast to string.
	 */
	- final const def valueToJson(self, value) {
		switch typeof value {
		case 'null':
			return 'null'
		case 'number':
		case 'boolean':
			return '%s' % value
		case 'string':
			return '"%s"' % Serializer.JsonStream.escapeString(value)
		case 'array':
			elements = []
			if self.indented {
				self.indentationLevel++
				for var item in value {
					elements << self.writeJson(item)
				}
				self.indentationLevel--
				if elements.isEmpty() {
					return '[]'
				} else {
					return '[\n%s\n%s]' % (elements.join(',\n'), ('\t' * self.indentationLevel))
				}
			} else {
				for var item in value {
					elements << self.writeJson(item)
				}
				return '[%s]' % elements.join(',')
			}
		case 'hash':
			elements = []
			if self.indented {
				self.indentationLevel++
				for let (key, item) in value {
					elements << '%s: %s' % (self.writeJson(key), self.valueToJson(item))
				}
				self.indentationLevel--
				if elements.isEmpty() {
					return '{}'
				} else {
					return '{\n%s\n%s}' % (elements.join(',\n'), ('\t' * self.indentationLevel))
				}
			} else {
				for let (key, item) in value {
					elements << '%s: %s' % (self.valueToJson(key), self.valueToJson(item))
				}
				return '{%s}' % elements.join(',')
			}
		default:
			for var member in ('toBoolean', 'toNumber', 'toString') {
				if defined value.${member} {
					return self.valueToJson(value.${member}())
				}
			}
			return '"%s"' % Serializer.JsonStream.escapeString(string(value))
		}
	}

	/**
	 * This class provides a parser to convert a JSON stream to built-in types.
	 * 
	 * The elements are converted as follow:
	 * * JSON objects are converted to hashes
	 * * JSON arrays are converted to arrays
	 * * JSON boolean values are converted to booleans
	 * * JSON numeric values are converted to numbers
	 * * JSON string values are converted to strings
	 * * JSON null value is converted to `null`
	 */
	- class Parser {
		/**
		 * Creates a new parser on the stream given by `stream`.
		 */
		const def new(self, stream) {
			self.stream = stream
			return self
		}

		/**
		 * Returns an instance of hash or array corresponding to the next JSON
		 * element in the stream.
		 * 
		 * An instance of {Serializer.DocumentStream.InvalidDocument} is raised
		 * on error.
		 */
		const def parse(self) {
			switch token = self.nextToken() {
			case '{':
				return self.parseNextObject()
			case '[':
				return self.parseNextArray()
			default:
				raise Serializer.DocumentStream.InvalidDocument(self.stream, "illegal value '%s'" % token)
			}
		}

		/**
		 * Returns the value converted from the next element of the stream.
		 * 
		 * An instance of {Serializer.DocumentStream.InvalidDocument} is raised on error.
		 */
		- final const def parseNextElement(self) {
			switch token = self.nextToken() {
			case '{':
				return self.parseNextObject()
			case '[':
				return self.parseNextArray()
			case '"':
				return self.parseNextString()
			case is none:
				raise Serializer.DocumentStream.InvalidDocument(self.stream, "unterminated element")
			default:
				if isDefined(value = self.parseNextValue(token)) {
					return value
				} else {
					raise Serializer.DocumentStream.InvalidDocument(self.stream, "unexpected token '%s'" % token)
				}
			}
		}

		/**
		 * Returns the value converted from the next object of the stream.
		 * 
		 * An instance of {Serializer.DocumentStream.InvalidDocument} is raised on error.
		 */
		- final const def parseNextObject(self) {
			var token = self.nextToken()
			var json_object = {}
			while token == '"' {
				let member = self.parseNextString()
				token = self.nextToken()
				if token != ':' {
					raise Serializer.DocumentStream.InvalidDocument(self.stream, "missing name separator")
				}
				let value = self.parseNextElement()
				token = self.nextToken()
				switch token {
				case ',':
					token = self.nextToken()
				case '}':
					json_object[member] = value
					break
				case is none:
					raise Serializer.DocumentStream.InvalidDocument(self.stream, "unexpected end of stream")
				default:
					raise Serializer.DocumentStream.InvalidDocument(self.stream, "missing value separator")
				}
			}
			if token != '}' {
				raise Serializer.DocumentStream.InvalidDocument(self.stream, "unterminated object")
			}
			return json_object
		}

		/**
		 * Returns the value converted from the next array of the stream.
		 * 
		 * An instance of {Serializer.DocumentStream.InvalidDocument} is raised
		 * on error.
		 */
		- final const def parseNextArray(self) {
			var token = self.nextToken()
			var json_array = []
			if token != ']' {
				self.discardToken()
				json_array << self.parseNextElement()
				token = self.nextToken()
				while token == ',' {
					json_array << self.parseNextElement()
					token = self.nextToken()
				}
				if token != ']' {
					raise Serializer.DocumentStream.InvalidDocument(self.stream, "unterminated array")
				}
			}
			return json_array
		}

		/**
		 * Returns the value converted from the next string of the stream.
		 * 
		 * An instance of {Serializer.DocumentStream.InvalidDocument} is raised
		 * on error.
		 */
		- final const def parseNextString(self) {
			var json_string = ''
			for (var c = self.nextChar(), c = self.nextChar(), defined c and c != '"') {
				switch c {
				case '\\':
					switch c = self.nextChar() {
					case '"':
				        json_string << '"'
				        break
				    case '\\':
				        json_string << '\\'
				        break
				    case 'b':
				        json_string << '\b'
				        break
				    case 'f':
				        json_string << '\f'
				        break
				    case 'n':
				        json_string << '\n'
				        break
				    case 'r':
				        json_string << '\r'
				        break
				    case 't':
				        json_string << '\t'
				        break
				    case 'u':
				        let code = '0x'
				        for i in 0...4 {
				        	c = self.nextChar()
				        	if c =~ /[0-9a-fA-F]/ {
				        		code << c
				        	} else {
				        		raise Serializer.DocumentStream.InvalidDocument(self.stream, "invalid escape sequence")
				        	}
				        }
				        json_string << ('%c' % number(code))
				        break
				    default:
				        json_string << c
					}
					break

				default:
					json_string << c
				}
			}
			if not defined c {
				raise Serializer.DocumentStream.InvalidDocument(self.stream, "unterminated string")
			}
			return json_string
		}

		/**
		 * Returns the value converted from the next value of the stream.
		 * 
		 * An instance of {Serializer.DocumentStream.InvalidDocument} is raised
		 * on error.
		 */
		- final const def parseNextValue(self, token) {
			if token =~ /\d/ or token == '-' or token == '+' {
				for (var c = self.nextChar(), c = self.nextChar(), c =~ /\d/) {
					token << c
				}
				self.discardToken()
				return number(token)
			} else {
				for (var c = self.nextChar(), c = self.nextChar(), c =~ /[a-z]/) {
					token << c
				}
				self.discardToken()
				switch token {
				case 'null':
					return null
				case 'false':
					return false
				case 'true':
					return true
				default:
					break
				}
			}
		}

		/**
		 * Discards the last token that was read. The same token will then be
		 * returned by the next call to {@see nextToken}.
		 */
		- final const def discardToken(self) {
			self.discarded = true
		}

		/**
		 * Returns the next token of the stream. A token is a character that does
		 * not match the `/\s/` regex.
		 */
		- final const def nextToken(self) {
			if self.discarded {
				self.discarded = false
				var c = self.c
			} else {
				var c = self.nextChar()
			}
			while defined c and c =~ /\s/ {
				c = self.nextChar()
			}
			return c
		}

		/**
		 * Returns the next character of the stream.
		 */
		- final const def nextChar(self) {
			return self.c = self.stream.readChar()
		}

		/// Internal stream object.
		- final stream = null

		/// Internal discarded state.
		- final discarded = false

		/// Internal last read character.
		- final c = none
	}

	/// Internal current indentation level.
	- final indentationLevel = 0

	/// Internal indentation format state.
	- final indented = true
}

}
