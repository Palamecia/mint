load mint.lang

class TextStream {
	def new(self, stream) {
		self.stream = self.createInternalStream(stream)
		return self
	}

	def getCodec(self) {
		if defined self.stream.getCodec {
			return self.stream.getCodec()
		}
	}

	def setCodec(self, codec) {
		if defined self.stream.setCodec {
			self.stream.setCodec(codec)
		}
	}

	def readChar(self) {
		if is_defined(c = self.stream.readChar()) {
			return string(c)
		}
	}

	def readWord(self) {
		if is_defined(word = self.stream.readWord()) {
			return string(word)
		}
	}

	def readLine(self) {
		if is_defined(line = self.stream.readLine()) {
			return string(line)
		}
	}

	def readLines(self) {
		for line in self.stream.readLines() {
			yield string(line)
		}
	}

	def read(self) {
		if is_defined(content = self.stream.read()) {
			return string(content)
		}
	}

	def write(self, data) {
		self.stream.write(string(data))
	}

	def <<(self, data) {
		self.write(data)
	}

	def isEmpty(self) {
		if defined self.stream.isEmpty {
			return self.stream.isEmpty()
		}
		if defined self.stream.isOver {
			return self.stream.isOver()
		}
		if defined self.stream.atEnd {
			return self.stream.atEnd()
		}
		return false
	}

	- def createInternalStream(self, stream) {

		if defined stream.read and defined stream.write {

			if defined stream.readChar and defined stream.readWord and defined stream.readLine and defined stream.readLines {
				return stream
			}

			load serializer.bufferedstream
			return BufferedStream(stream)
		}

		if defined stream.readString and (defined stream.readDataStream or defined stream.readUInt8) and defined stream.write {
			load serializer.bufferedstream
			return BufferedStream(stream)
		}

		load serializer.stringstream
		return StringStream(stream)
	}

	- stream = null
}
