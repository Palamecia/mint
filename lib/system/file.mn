load system.directory

/*!
 * @class File
 */
class File {

	/*!
	 * @brief File constructor
	 * @param path file's path
	 * @return file object
	 */
	def new(self, path) {
		self.path = path
		return self
	}

	/*!
	 * @brief File destructor
	 */
	def delete(self) {
		if self.obj {
			File.g_lib.call('mint_file_fclose', self.obj)
		}
	}

	/*!
	 * @return
	 */
	def getPath(self) {
		return self.path
	}

	/*!
	 * @brief
	 * @param mode file open mode
	 */
	def open(self, mode = 'r') {
		if self.obj is null {
			if self.obj = File.g_lib.call('mint_file_fopen', self.path, mode) {
				return true
			}
		}
		return false
	}

	/*!
	 * @brief
	 */
	def close(self) {
		if self.obj {
			File.g_lib.call('mint_file_fclose', self.obj)
			self.obj = null
		}
	}

	/*!
	 * @return
	 */
	def isOpen(self) {
		return self.obj != null
	}

	/*!
	 * @brief read a character from a file
	 */
	def readChar(self) {
		if self.obj {
			return File.g_lib.call('mint_file_fgetc', self.obj)
		}
	}

	/*!
	 * @brief read a line from a file
	 */
	def readLine(self) {
		if self.obj {
			return File.g_lib.call('mint_file_readline', self.obj)
		}
	}

	/*!
	 * @brief read all the content of a file
	 */
	def read(self) {
		content = ''
		while line = self.readLine() {
			content += line + '\n'
		}
		return content
	}

	- @g_lib = lib('libmint-system')
	- path = ''
	- obj = null
}

/*!
 * @brief
 * @param path 
 * @param mode 
 */
def open(path, mode = 'r') {
	file = File(path)
	if file.open(mode) {
		return file
	}
	return null
}
